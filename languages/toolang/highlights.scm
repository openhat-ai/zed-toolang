; Source of truth for Toolang Tree-sitter highlight captures.
; Synced copy for the Zed extension. Edit the grammar repository instead.

(comment) @comment
(inline_comment) @comment

(use_keyword) @keyword
(struct_keyword) @keyword
(thunk_keyword) @keyword
(decl_kind) @keyword

(cap_kind) @type
(collection_subject) @property
(model_subject) @property
(language) @property

(assign_operator) @operator
(add_assign_operator) @operator
(remove_assign_operator) @operator
(arrow) @operator

(colon) @punctuation.delimiter
(comma) @punctuation.delimiter
(lparen) @punctuation.delimiter
(rparen) @punctuation.delimiter
(question) @punctuation.special
(array_suffix) @punctuation.special
(fence_open) @punctuation.special
(fence_close) @punctuation.special

(reference) @constant
(body_text) @string
(fence_text) @string

(collection_directive
  values: (directive_values
    (directive_value) @constant))

(model_directive
  values: (directive_values
    (directive_value) @type))

(declaration_header
  kind: (decl_kind) @_kind
  name: (identifier) @function
  (#eq? @_kind "slash"))

(declaration_header
  kind: (decl_kind) @_kind
  name: (identifier) @type
  (#any-of? @_kind "psyche" "service"))

(struct_header
  name: (identifier) @type)

(thunk_header
  name: (identifier) @function)

(struct_field
  name: (identifier) @property)

(parameter
  name: (named_identifier) @property)

(parameter
  name: (underscore) @property)

(type_expression
  name: (identifier) @type)

(use_statement
  reference: (reference) @constant)
